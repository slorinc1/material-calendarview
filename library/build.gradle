apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
  compileSdkVersion 30
  buildToolsVersion "30.0.1"

  defaultConfig {
    minSdkVersion 26
    targetSdkVersion 30
    versionCode 3
    versionName '3.0'

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

group = "com.github.prolificinteractive"
version = android.defaultConfig.versionName
archivesBaseName = 'material-calendarview'

def hamcrestVersion = '1.3'
def junitVersion = '4.12'
def runnerVersion = '0.5'
def supportVersion = '27.0.2'

def supportV4 = "com.android.support:support-v4:$supportVersion"
def supportDesign = "com.android.support:design:$supportVersion"
def supportAnnotations = "com.android.support:support-annotations:$supportVersion"
def supportAppCompat = "com.android.support:appcompat-v7:$supportVersion"
def recyclerviewV7 = "com.android.support:recyclerview-v7:$supportVersion"

// Hamcrest
def hamcrestCore = "org.hamcrest:hamcrest-core:$hamcrestVersion"
def hamcrestLibrary = "org.hamcrest:hamcrest-library:$hamcrestVersion"
def hamcrestIntegration = "org.hamcrest:hamcrest-integration:$hamcrestVersion"

// Other
def threeTenAbp = 'com.jakewharton.threetenabp:threetenabp:1.1.1'
def butterknife = 'com.jakewharton:butterknife:8.5.1'
def butterknifeCompiler = 'com.jakewharton:butterknife-compiler:8.5.1'

dependencies {
  implementation threeTenAbp
  implementation supportV4
  implementation supportDesign

  implementation supportAppCompat
  implementation supportAnnotations
  implementation 'org.jetbrains:annotations:15.0'
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('encoding', 'UTF-8')
  options.addStringOption('charSet', 'UTF-8')
}

android.libraryVariants.all { variant ->
  task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
    title "Material CalendarView ${version}"
    description "Generates Javadoc for $variant.name."
    source = variant.javaCompiler.source
    doFirst {
      classpath = files(variant.javaCompiler.classpath.files, project.android.getBootClasspath())
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    options {
      links "http://docs.oracle.com/javase/7/docs/api/"
      linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
  }
}